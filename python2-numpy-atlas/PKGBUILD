# $Id: PKGBUILD 182931 2013-04-16 08:33:21Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve> 
# Tweaked for ATLAS by: Cory Giles <cory.b.giles@gmail.com>

pkgname=('python-numpy-atlas' 'python2-numpy-atlas')
pkgver=1.7.1
pkgrel=1
pkgdesc="Scientific tools for Python"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.numpy.org/"
depends=('atlas-lapack>=3.10.1')
makedepends=('gcc-fortran' 'atlas-lapack>=3.10.1')
source=(http://downloads.sourceforge.net/numpy/numpy-${pkgver}.tar.gz)
md5sums=('0ab72b3b83528a7ae79c6df9042d61c6')

build() {
  cd "${srcdir}"
  cp -a numpy-${pkgver} numpy-py2-${pkgver}
}

package_python2-numpy-atlas() {
  local depends=("${depends[@]}" 'python2')
  local makedepends=("${makedepends[@]}" 
    'python2' 'python2-distribute' 'python2-nose')
  optdepends=('python-nose: testsuite')
  provides=("python2-numpy=${pkgver}")
  replaces=('python2-numpy')
  conflicts=('python2-numpy')

  _pyver=2.7

  export LDFLAGS="$LDFLAGS -shared"

  cd "${srcdir}/numpy-py2-${pkgver}"
  python2 setup.py config_fc --fcompiler=gnu95 install --prefix=/usr --root="${pkgdir}" --optimize=1

  install -m755 -d "${pkgdir}/usr/share/licenses/python2-numpy"
  install -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/python2-numpy/"

  sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
         -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
         -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
             $(find ${pkgdir} -name '*.py')

  install -m755 -d "${pkgdir}/usr/include/python${_pyver}"
  ln -sf /usr/lib/python${_pyver}/site-packages/numpy/core/include/numpy "${pkgdir}/usr/include/python${_pyver}/numpy"
}

package_python-numpy-atlas() {
  local depends=("${depends[@]}" 'python')
  local makedepends=("${makedepends[@]}" 
    'python' 'python-distribute' 'python-nose')
  optdepends=('python-nose: testsuite')

  provides=("python3-numpy=${pkgver}")
  replaces=('python3-numpy')
  conflicts=('python3-numpy')

  _pyver=3.3
  _pyinc=3.3m

  export LDFLAGS="$LDFLAGS -shared"

  cd "${srcdir}/numpy-${pkgver}"
  python setup.py config_fc --fcompiler=gnu95 install --prefix=/usr --root="${pkgdir}" --optimize=1

  install -m755 -d "${pkgdir}/usr/share/licenses/python-numpy"
  install -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/python-numpy/"

  install -m755 -d "${pkgdir}/usr/include/python${_pyinc}"
  ln -sf /usr/lib/python${_pyver}/site-packages/numpy/core/include/numpy "${pkgdir}/usr/include/python${_pyinc}/numpy"
}
